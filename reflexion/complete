PROGRAM          : LINEBREAK COMPLETE_COMMANDS LINEBREAK
                 | LINEBREAK
                 ;
COMPLETE_COMMANDS: COMPLETE_COMMANDS NEWLINE_LIST COMPLETE_COMMAND
                 |                                COMPLETE_COMMAND
                 ;
COMPLETE_COMMAND : LIST SEPARATOR_OP
                 | LIST
                 ;
LIST             : LIST SEPARATOR_OP AND_OR
                 |                   AND_OR
                 ;
AND_OR           :                         PIPELINE
                 | AND_OR and_if LINEBREAK PIPELINE
                 | AND_OR or_if  LINEBREAK PIPELINE
                 ;
PIPELINE         :      PIPE_SEQUENCE
                 | bang PIPE_SEQUENCE
                 ;
PIPE_SEQUENCE    :                             COMMAND
                 | PIPE_SEQUENCE '|' LINEBREAK COMMAND
                 ;
COMMAND          : SIMPLE_COMMAND
                 | COMPOUND_COMMAND
                 | COMPOUND_COMMAND REDIRECT_LIST
                 | FUNCTION_DEFINITION
                 ;
COMPOUND_COMMAND : BRACE_GROUP
                 | SUBSHELL
                 | FOR_CLAUSE
                 | CASE_CLAUSE
                 | IF_CLAUSE
                 | WHILE_CLAUSE
                 | UNTIL_CLAUSE
                 ;
SUBSHELL         : '(' COMPOUND_LIST ')'
                 ;
COMPOUND_LIST    : LINEBREAK TERM
                 | LINEBREAK TERM SEPARATOR
                 ;
TERM             : TERM SEPARATOR AND_OR
                 |                AND_OR
                 ;
FOR_CLAUSE       : for NAME                                      DO_GROUP
                 | for NAME                       SEQUENTIAL_SEP DO_GROUP
                 | for NAME LINEBREAK IN          SEQUENTIAL_SEP DO_GROUP
                 | for NAME LINEBREAK IN WORDLIST SEQUENTIAL_SEP DO_GROUP
                 ;
NAME             : name                     
                 ;
IN               : in                       
                 ;
WORDLIST         : WORDLIST word
                 |          word
                 ;
CASE_CLAUSE      : case word LINEBREAK IN LINEBREAK CASE_LIST    esac
                 | case word LINEBREAK IN LINEBREAK CASE_LIST_NS esac
                 | case word LINEBREAK IN LINEBREAK              esac
                 ;
CASE_LIST_NS     : CASE_LIST CASE_ITEM_NS
                 |           CASE_ITEM_NS
                 ;
CASE_LIST        : CASE_LIST CASE_ITEM
                 |           CASE_ITEM
                 ;
CASE_ITEM_NS     :     PATTERN ')' LINEBREAK
                 |     PATTERN ')' COMPOUND_LIST
                 | '(' PATTERN ')' LINEBREAK
                 | '(' PATTERN ')' COMPOUND_LIST
                 ;
CASE_ITEM        :     PATTERN ')' LINEBREAK     dsemi LINEBREAK
                 |     PATTERN ')' COMPOUND_LIST dsemi LINEBREAK
                 | '(' PATTERN ')' LINEBREAK     dsemi LINEBREAK
                 | '(' PATTERN ')' COMPOUND_LIST dsemi LINEBREAK
                 ;
PATTERN          :             word         
                 | PATTERN '|' word         
                 ;
IF_CLAUSE        : if COMPOUND_LIST then COMPOUND_LIST ELSE_PART fi
                 | if COMPOUND_LIST then COMPOUND_LIST           fi
                 ;
ELSE_PART        : elif COMPOUND_LIST then COMPOUND_LIST
                 | elif COMPOUND_LIST then COMPOUND_LIST ELSE_PART
                 | else COMPOUND_LIST
                 ;
WHILE_CLAUSE     : while COMPOUND_LIST DO_GROUP
                 ;
UNTIL_CLAUSE     : until COMPOUND_LIST DO_GROUP
                 ;
FUNCTION_DEFINITION : FNAME '(' ')' LINEBREAK FUNCTION_BODY
                 ;
FUNCTION_BODY    : COMPOUND_COMMAND                
                 | COMPOUND_COMMAND REDIRECT_LIST  
                 ;
FNAME            : name                            
                 ;
BRACE_GROUP      : lbrace COMPOUND_LIST rbrace
                 ;
DO_GROUP         : do COMPOUND_LIST done           
                 ;
SIMPLE_COMMAND   : CMD_PREFIX CMD_WORD CMD_SUFFIX
                 | CMD_PREFIX CMD_WORD
                 | CMD_PREFIX
                 | CMD_NAME CMD_SUFFIX
                 | CMD_NAME
                 ;
CMD_NAME         : word                   
                 ;
CMD_WORD         : word                   
                 ;
CMD_PREFIX       :            IO_REDIRECT
                 | CMD_PREFIX IO_REDIRECT
                 |            assignment_word
                 | CMD_PREFIX assignment_word
                 ;
CMD_SUFFIX       :            IO_REDIRECT
                 | CMD_SUFFIX IO_REDIRECT
                 |            word
                 | CMD_SUFFIX word
                 ;
REDIRECT_LIST    :               IO_REDIRECT
                 | REDIRECT_LIST IO_REDIRECT
                 ;
IO_REDIRECT      :           IO_FILE
                 | io_number IO_FILE
                 |           IO_HERE
                 | io_number IO_HERE
                 ;
IO_FILE          : '<'       FILENAME
                 | lessand   FILENAME
                 | '>'       FILENAME
                 | greatand  FILENAME
                 | dgreat    FILENAME
                 | lessgreat FILENAME
                 | clobber   FILENAME
                 ;
FILENAME         : word                      
                 ;
IO_HERE          : dless     HERE_END
                 | dlessdash HERE_END
                 ;
HERE_END         : word                      
                 ;
NEWLINE_LIST     :              newline
                 | NEWLINE_LIST newline
                 ;
LINEBREAK        : NEWLINE_LIST
                 | EMPTY
                 ;
SEPARATOR_OP     : '&'
                 | ';'
                 ;
SEPARATOR        : SEPARATOR_OP LINEBREAK
                 | NEWLINE_LIST
                 ;
SEQUENTIAL_SEP   : ';' LINEBREAK
                 | NEWLINE_LIST
                 ;
